; Item Cards - Stentorious

; Aux Var
; "*ItemCard_INI"
; 0 = Keybind code
; 1 = Rebuild armor effect description
; 2 = Show ammo item cards
; 3 = Show weapon mod item cards
; 4 = Always show item cards

; On game load
SetUIFloatGradual "HUDMainMenu\IC\_alpha" 0
SetUIFloatGradual "HUDMainMenu\IC\CleanBackground\alpha" 0

; On game restart
if eval Goo1.AuxVarGetFlt "*ItemCard_Init"
	return
endif
Goo1.AuxVarSetFlt "*ItemCard_Init" 1

; Check for requirements
if GetNVSEVersion > 5 && GetNVSERevision > 2 && GetNVSEBeta > 9
else
   MessageBoxEx "Item Cards missing requirement!%rInstall xNVSE 6.3.10+."
   return
endif
if GetPluginVersion "JohnnyGuitarNVSE" < 200
	MessageBoxEx "Item Cards missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
	return
endif
if GetPluginVersion "ShowOffNVSE Plugin" < 145
	MessageBoxEx "Item Cards missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif
if GetPluginVersion "lStewieAl's Tweaks" < 960
	MessageBoxEx "Item Cards missing requirement!%rInstall latest lStewieAl's Tweaks."
	return
endif
if eval GetPluginVersion "yUI" >= 140 && FileExists "menus\ySI\ySI.xml"
	Goo1.AuxVarSetFlt "*_ySI-SortingIcons" 1
endif

; Load INI config
Goo1.AuxVarSetFlt "*ItemCard_INI" (GetINIFloat_Cached "General:iKeybind" "Stentorious\ItemCards.ini") 0
Goo1.AuxVarSetFlt "*ItemCard_INI" (GetINIFloat_Cached "General:bRebuildArmorEffects" "Stentorious\ItemCards.ini") 1
Goo1.AuxVarSetFlt "*ItemCard_INI" (GetINIFloat_Cached "General:bAmmoItemCards" "Stentorious\ItemCards.ini") 2
Goo1.AuxVarSetFlt "*ItemCard_INI" (GetINIFloat_Cached "General:bWeaponModItemCards" "Stentorious\ItemCards.ini") 3
Goo1.AuxVarSetFlt "*ItemCard_INI" (GetINIFloat_Cached "General:bShowAlways" "Stentorious\ItemCards.ini") 4

; B42 Inspect compatibility
if eval IsModLoaded "B42Inspect.esp" && GetINIFloat_Cached "General:bB42Inspect" "Stentorious\ItemCards.ini"
	Goo1.AuxVarSetRef "*B42IQuest" (EditorIDToFormID "B42InspectQ")
	SetGameMainLoopCallBack (CompileScript "ItemCards\stUDFItemCardsB42Callback.gek") 1 1 1
endif

; lStewieAl's Tweaks Auxiliary-Variables
; "bCritChanceIgnoresFireRate"
Goo1.AuxVarSetFlt "*_Tweaks:bCritChanceIgnoresFireRate" (StewVal "bCritChanceIgnoresFireRate:Tweaks") 0

; "bScaleCriticalDamage"
Goo1.AuxVarSetFlt "*_Tweaks:bScaleCriticalDamage" (StewVal "bScaleCriticalDamage:Tweaks") 0
if eval Goo1.AuxVarGetFlt "*_Tweaks:bScaleCriticalDamage" 0
	Goo1.AuxVarSetFlt "*_Tweaks:bScaleCriticalDamage" (StewVal "fCriticalDamageMult:Critical Hits") 1
else
	Goo1.AuxVarSetFlt "*_Tweaks:bScaleCriticalDamage" 1 1
endif

; "bHardcoreTweaks"
if eval StewVal "bHardcoreTweaks:Tweaks"
	Goo1.AuxVarSetFlt "*_Tweaks:bHardcoreTweaks" 1 0
	Goo1.AuxVarSetFlt "*_Tweaks:bHardcoreTweaks" (StewVal "bAmmoWeight:Hardcore Tweaks") 1
endif

; "bWeightlessItems"
if eval StewVal "bWeightlessItems:Tweaks"
	Goo1.AuxVarSetFlt "*_Tweaks:bWeightlessItems" 1 0
	Goo1.AuxVarSetFlt "*_Tweaks:bWeightlessItems" (1 - (StewVal "bAid:Weightless Items")) 1
	Goo1.AuxVarSetFlt "*_Tweaks:bWeightlessItems" (1 - (StewVal "bAmmo:Weightless Items")) 2
	Goo1.AuxVarSetFlt "*_Tweaks:bWeightlessItems" (1 - (StewVal "bArmor:Weightless Items")) 3
	Goo1.AuxVarSetFlt "*_Tweaks:bWeightlessItems" (1 - (StewVal "bMisc:Weightless Items")) 4
	Goo1.AuxVarSetFlt "*_Tweaks:bWeightlessItems" (1 - (StewVal "bWeapons:Weightless Items")) 5
endif

; Inject item card
InjectUIXML "HUDMainMenu" "data\menus\prefabs\Stentorious\ItemCards.xml"
SetUIFloatAlt "HUDMainMenu\IC\x" (GetINIFloat_Cached "Interface:fCardPosHorizontal" "Stentorious\ItemCards.ini")
SetUIFloatAlt "HUDMainMenu\IC\y" (GetINIFloat_Cached "Interface:fCardPosVertical" "Stentorious\ItemCards.ini")
SetUIFloatAlt "HUDMainMenu\IC\_BGAlpha" (GetMaxOf 0 (GetMinOf 1 (GetINIFloat_Cached "Interface:fBackgroundOpacity" "Stentorious\ItemCards.ini")))
SetUIFloatAlt "HUDMainMenu\IC\width" (GetUIFloatAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ItemInfoRect\width")
SetUIFloatAlt "HUDMainMenu\IC\height" (GetUIFloatAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ItemInfoRect\height")
SetUIFloatAlt "HUDMainMenu\IC\MI1\y" (GetUIFloatAlt "HUDMainMenu\IC\CND\y" * 2)
SetUIFloatAlt "HUDMainMenu\IC\MI2\y" (GetUIFloatAlt "HUDMainMenu\IC\CND\y" * 3)
SetUIFloatAlt "HUDMainMenu\IC\MI3\y" (GetUIFloatAlt "HUDMainMenu\IC\CND\y" * 4)
SetUIFloatAlt "HUDMainMenu\IC\DAM\CI_ValueText\wrapwidth" 100
SetUIFloatAlt "HUDMainMenu\IC\CHD\CI_ValueText\wrapwidth" 100
SetUIFloatAlt "HUDMainMenu\IC\CHC\CI_ValueText\wrapwidth" 100
SetUIFloatAlt "HUDMainMenu\IC\MI1\CI_ValueText\wrapwidth" 500
SetUIFloatAlt "HUDMainMenu\IC\MI2\CI_ValueText\wrapwidth" 500
SetUIFloatAlt "HUDMainMenu\IC\MI3\CI_ValueText\wrapwidth" 500
SetUIFloatAlt "HUDMainMenu\IC\MI2\CI_TopLine\visible" 0
SetUIFloatAlt "HUDMainMenu\IC\MI3\CI_TopLine\visible" 0
SetUIFloatAlt "HUDMainMenu\IC\MI2\CI_RightVert\visible" 0
SetUIFloatAlt "HUDMainMenu\IC\MI3\CI_RightVert\visible" 0
SetUIStringAlt "HUDMainMenu\IC\DPS\_value" "--"

; Modded HUD support
if eval IsModLoaded "DarNifiedUINV.esp"
	SetUIFloatAlt "HUDMainMenu\IC\_BGOffset" 0
	SetUIFloatAlt "HUDMainMenu\IC\_ModOffset" 24
	SetUIFloatAlt "HUDMainMenu\IC\CND\ConditionMeter\height" 15
	SetUIFloatAlt "HUDMainMenu\IC\CND\CNDArrows\height" 15
	SetUIFloatAlt "HUDMainMenu\IC\CND\CNDArrows\width" 15
	SetUIFloatAlt "HUDMainMenu\IC\EI\CI_ValueText\y" 9.5
elseif eval IsModLoaded "Vanilla UI Plus.esp"
	SetUIFloatAlt "HUDMainMenu\IC\DAM\_VUI+NoLabel" 1
	SetUIFloatAlt "HUDMainMenu\IC\MI1\_VUI+NoLabel" 1
	if eval FileExists "textures\VUIE\Brackets\Bracket.dds"
		SetUIFloatAlt "HUDMainMenu\IC\font" 3
		SetUIFloatAlt "HUDMainMenu\IC\_BGOffset" -2
		SetUIFloatAlt "HUDMainMenu\IC\_ModOffset" 24
		SetUIFloatAlt "HUDMainMenu\IC\CND\ConditionMeter\height" 16
		SetUIFloatAlt "HUDMainMenu\IC\CND\CNDArrows\width" 15
		SetUIFloatAlt "HUDMainMenu\IC\CND\CNDArrows\height" 16
		ModUIFloat "HUDMainMenu\IC\CND\ConditionMeter\y" -2.25
	endif
else
	SetUIFloatAlt "HUDMainMenu\IC\_BGOffset" 5
	ModUIFloat "HUDMainMenu\IC\CND\ConditionMeter\y" 3.75
	SetUIFloatAlt "HUDMainMenu\IC\MI1\CI_ValueText\wrapwidth" 400
	ModUIFloat "HUDMainMenu\IC\SKL\Icon\y" -2
endif

; Enable extra weapon stat info
if eval (GetINIFloat_Cached "General:bExtraWeaponStats" "Stentorious\ItemCards.ini") != 1
	SetUIFloatAlt "HUDMainMenu\IC\CHD\visible" 0
endif

; Enable colorful CND
if eval (GetINIFloat_Cached "Color:iCNDMeter" "Stentorious\PipBoyUITweaks.ini") > 0
	InjectUIXML "HUDMainMenu\IC\CND\ConditionMeter" "data\menus\prefabs\Stentorious\PipBoyUITweaks\ColorCNDMeterIC.xml"
	SetUIFloatAlt "HUDMainMenu\IC\CND\ConditionMeter\MeterImage\visible" 0
endif

; ySI - Sorting Icons support
if eval Goo1.AuxVarGetFlt "*_ySI-SortingIcons"

	; Enable ySI icons (WeightInfo)
	if eval GetINIFloat_Cached "ySI:bWeightInfoIcon" "Stentorious\PipBoyUITweaks.ini"
		InjectUIXML "HUDMainMenu\IC\WG" "data\menus\prefabs\Stentorious\ySICapIcon.xml"
		SetUIFloatAlt "HUDMainMenu\IC\WG\CI_TitleText\visible" 0
		SetUIFloatAlt "HUDMainMenu\IC\WG\ySIImage\x" (GetUIFloatAlt "HUDMainMenu\IC\WG\CI_TitleText\x")
		SetUIStringAlt "HUDMainMenu\IC\WG\ySIImage\filename" "interface\icons\Apparel_Other_Backpack.dds"
	endif

	; Enable ySI icons (ValueInfo)
	if eval GetINIFloat_Cached "ySI:bValueInfoIcon" "Stentorious\PipBoyUITweaks.ini"
		InjectUIXML "HUDMainMenu\IC\VAL" "data\menus\prefabs\Stentorious\ySICapIcon.xml"
		SetUIFloatAlt "HUDMainMenu\IC\VAL\CI_TitleText\visible" 0
		SetUIFloatAlt "HUDMainMenu\IC\VAL\ySIImage\x" (GetUIFloatAlt "HUDMainMenu\IC\VAL\CI_TitleText\x")
	endif

	; Enable ySI icons (AmmoInfo)
	if eval GetINIFloat_Cached "ySI:bAmmoInfoIcon" "Stentorious\PipBoyUITweaks.ini"
		InjectUIXML "HUDMainMenu\IC\AMMO" "data\menus\prefabs\Stentorious\ySIAmmoIcon.xml"
		ModUIFloat "HUDMainMenu\IC\AMMO\CI_TitleText\x" ((GetUIFloatAlt "HUDMainMenu\IC\AMMO\ySIImage\width") + 8)
	endif

endif

; Clear INI cache
CloseFileSO "Stentorious\ItemCards.ini" 0
CloseFileSO "Stentorious\PipBoyUITweaks.ini" 0

; Item card update event handlers
SetEventHandler "OnCrosshairOn" (CompileScript "ItemCards\stUDFItemCardsOnCrosshairOn.gek")
SetEventHandler "OnCrosshairOff" (CompileScript "ItemCards\stUDFItemCardsOnCrosshairOff.gek")
SetEventHandler "ShowOff:OnAdd" (CompileScript "ItemCards\stUDFItemCardsOnAdd.gek") "second"::player
if eval Goo1.AuxVarGetFlt "*ItemCard_INI" 4 != 1
	SetOnKeyDownEventHandler (CompileScript "ItemCards\stUDFItemCardsOnKeyDown42.gek") 1 (Goo1.AuxVarGetFlt "*ItemCard_INI" 0)
	SetOnKeyUpEventHandler (CompileScript "ItemCards\stUDFItemCardsOnKeyUp42.gek") 1 (Goo1.AuxVarGetFlt "*ItemCard_INI" 0)
endif

; JLM support
if eval IsModLoaded "JustAssortedMods.esp" || IsModLoaded "JustLootMenu.esp"
	Goo1.AuxVarSetRef "*JLMQuest" (EditorIDToFormID "JLM")
	SetOnKeyDownEventHandler (CompileScript "ItemCards\stUDFItemCardsOnKeyDown264265.gek") 1 264
	SetOnKeyDownEventHandler (CompileScript "ItemCards\stUDFItemCardsOnKeyDown264265.gek") 1 265
	SetEventHandler "OnButtonDown:3" (CompileScript "ItemCards\stUDFItemCardsOnKeyDown264265.gek")
endif

; MAPMO Support
if eval IsModLoaded "Main And Pause Menus Overhaul.esm"
	Goo1.AuxVarSetRef "*MAPMOQuest" (EditorIDToFormID "MAPMOMainQUEST")
	SetGameMainLoopCallBack (CompileScript "ItemCards\stUDFItemCardsMAPMOCallback.gek") 1 1 1
endif
